buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // https://s3.amazonaws.com/fabric-artifacts/public/io/fabric/tools/gradle/maven-metadata.xml
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def getBuildDateTime() {
    return project.hasProperty('devBuild') ? "001" : new Date().format('yyyyMMddHHmm')
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.tools

    defaultConfig {
        applicationId "com.ubidel.ubicash"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug" + getBuildDateTime()
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        development {
            minSdkVersion 21
            resConfigs("en", "xxhdpi")

        }

        production {
            minSdkVersion 16
        }
    }

    if (project.hasProperty('devBuild')) {
        splits.abi.enable = false
        splits.density.enable = false
        aaptOptions.cruncherEnabled = false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Android support library
    compile rootProject.ext.supportV4
    compile rootProject.ext.design
    compile rootProject.ext.appCompat
    compile rootProject.ext.cardView
    compile rootProject.ext.recyclerView
    compile rootProject.ext.constraintLayout

    // 3rd Party Libraries
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'
    compile 'com.google.dagger:dagger:2.4'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.4'
    compile rootProject.ext.retrofit
    compile rootProject.ext.converter_gson
    compile(rootProject.ext.adapter_rxjava) {
        exclude module: 'io.reactivex:rxjava:1.3.0@jar'
    }
    compile rootProject.ext.logging_interceptor
    compile rootProject.ext.okhttp
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'com.artemzin.rxjava:proguard-rules:1.3.0.0'
    compile 'com.github.bumptech.glide:glide:4.0.0-RC0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0-RC0'
    compile 'com.github.kenglxn.QRGen:android:2.2.0'
    compile 'me.dm7.barcodescanner:zxing:1.9'
    compile 'com.hannesdorfmann.mosby3:mvi:3.0.4' // Model-View-Intent
    compile 'com.hannesdorfmann.mosby3:mvp:3.0.4' // Plain MVP
    compile 'com.hannesdorfmann.mosby3:viewstate:3.0.4' // MVP + ViewState support
    compile 'com.hannesdorfmann.mosby3:mvp-nullobject-presenter:3.0.4'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'net.danlew:android.joda:2.9.9'
    provided 'org.projectlombok:lombok:1.16.16'
    annotationProcessor 'org.projectlombok:lombok:1.16.16'
    compile 'com.scottyab:secure-preferences-lib:0.1.4'
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    testCompile 'junit:junit:4.12'

}
